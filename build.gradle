plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "jacoco"
    id "com.glovoapp.semantic-versioning" version "1.1.10"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.dokka" version "1.7.20"
    id "com.github.nbaztec.coveralls-jacoco" version "1.2.15"
}

group = "io.resoluteworks"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadocJar {
    dependsOn(dokkaHtml)
    archiveClassifier.set("javadoc")
    from(dokkaHtml.outputDirectory)
}

configurations.all {
    resolutionStrategy {
        force 'it.unibz.inf.ontop:ontop-rdf4j-config-sql:4.2.2'
        force 'it.unibz.inf.ontop:ontop-ontology-owlapi:4.2.2'
        force 'it.unibz.inf.ontop:ontop-system-sql-owlapi:4.2.2'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.slf4j:slf4j-api:1.7.36"
    implementation "org.eclipse.rdf4j:rdf4j-rio-api:4.3.2"
    implementation "org.eclipse.rdf4j:rdf4j-runtime:4.3.2"

    testImplementation "io.kotest:kotest-assertions-core:${kotestVersion}"
    testImplementation "io.kotest:kotest-property:${kotestVersion}"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:${kotestVersion}"
    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    testImplementation "org.awaitility:awaitility:4.2.0"
    testImplementation "ch.qos.logback:logback-classic:1.4.5"
    testImplementation "io.github.serpro69:kotlin-faker:1.10.0"
    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation "com.ontotext.graphdb:graphdb-runtime:10.2.1"
}

kotlin {
    jvmToolchain(11)
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    systemProperty "kotest.assertions.collection.print.size", 1000
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "rdf4k"
                description = "A Kotlin library for processing RDF"
                url = "https://github.com/${publishGit}"
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = "https://github.com/${publishGit}/blob/main/LICENSE"
                        distribution = 'repo'
                    }
                }
                scm {
                    url = "https://github.com/${publishGit}"
                    connection = "scm:git:git://github.com/${publishGit}.git"
                    developerConnection = "scm:git:ssh://git@github.com:${publishGit}.git"
                }
                developers {
                    developer {
                        name = 'Cosmin Marginean'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
//        maven {
//            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
//            credentials {
//                username = "${NEXUS_USERNAME}"
//                password = "${NEXUS_PASSWORD}"
//            }
//        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task fullBuild {
    dependsOn 'clean'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
    dependsOn 'coverallsJacoco'
    tasks.findByName('jacocoTestReport').mustRunAfter 'test'
    tasks.findByName('coverallsJacoco').mustRunAfter 'jacocoTestReport'
}
